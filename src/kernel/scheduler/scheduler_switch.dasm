; At this point: PEEK = A, PICK 1 = PC
:scheduler_switch
    ;brk 0
    ife [nProcesses], 1
        set PC, scheduler_switch_end

    set A, [processes]
    add A, [running_process]
    set A, [A]

    set [A], POP
    set [A + 1], B
    set [A + 2], C
    set [A + 3], X
    set [A + 4], Y
    set [A + 5], Z
    set [A + 6], I
    set [A + 7], J
    set [A + 8], EX
    set [A + 9], PEEK
    set [A + 10], SP
    add [A + 10], 1 ; PC is still on the stack

    :scheduler_abort

    add [running_process], 1
    ife [running_process], [nProcesses]
        set [running_process], 0
    set A, [processes]
    add A, [running_process]
    set A, [A]

    set [__scheduler_buffer1], [A + 10]
    set [__scheduler_buffer2], [A + 9]

    set PUSH, [A]
    set B, [A + 1]
    set C, [A + 2]
    set X, [A + 3]
    set Y, [A + 4]
    set Z, [A + 5]
    set I, [A + 6]
    set J, [A + 7]
    set EX, [A + 8]
    set A, POP
    set SP, [__scheduler_buffer1]

    ; We do an "RFI" by ourselves since we f*cked up the stack
    set PUSH, [__scheduler_buffer2]
    set PUSH, A

    :scheduler_switch_end
    rfi 0
